plugins {
    id 'java'
    id 'pmd'
    id 'checkstyle'
    id 'jacoco'
    id "com.diffplug.spotless"
}


group 'io.github.awsjavakit'
version = '0.29.1'

repositories {
    mavenCentral()
    maven { url = "https://s3.eu-central-1.amazonaws.com/dynamodb-local-frankfurt/release" }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

test {
    failFast = true
    useJUnitPlatform {
        excludeTags("online", "long")

    }
    finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}


jacoco {
    toolVersion = "0.8.12"
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            enabled = true
            limit {
                minimum = 1.000
                counter = 'METHOD'
                value = 'COVEREDRATIO'
            }
        }

        rule {
            enabled = true
            limit {
                minimum = 1.000
                counter = 'CLASS'
                value = 'COVEREDRATIO'
            }
        }


    }
}
check.dependsOn jacocoTestCoverageVerification
jacocoTestCoverageVerification.dependsOn(jacocoTestReport)

pmd {
    consoleOutput = true
    toolVersion = "7.5.0"
    rulesMinimumPriority = 5
    ruleSets = ["${rootProject.getRootDir()}/qa_config/pmd/ruleset.xml"]
}


tasks.withType(Checkstyle) {
    ignoreFailures = false
    showViolations = true
    config = resources.text.fromFile("${rootProject.getRootDir()}/qa_config/checkstyle/checkstyle.xml")
    reports {
        xml.required = false
        html.required = true
        html.stylesheet resources.text.fromFile("${rootProject.getRootDir()}/qa_config/checkstyle/checkstyle-frames.xsl")
    }
}



spotless{
  java {
    removeUnusedImports()
  }
}

