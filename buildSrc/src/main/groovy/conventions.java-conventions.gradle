plugins {
    id 'java'
    id 'java-library'
    id 'pmd'
    id 'checkstyle'
    id 'jacoco'
}

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

test {
    failFast = false
    useJUnitPlatform {
        includeEngines("junit-jupiter")
    }
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}


jacoco {
    toolVersion = "0.8.8"
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            enabled = true
            limit {
                minimum = 1.000
                counter = 'METHOD'
                value = 'COVEREDRATIO'
            }
        }

        rule {
            enabled = true
            limit {
                minimum = 1.000
                counter = 'CLASS'
                value = 'COVEREDRATIO'
            }
        }


    }
}
check.dependsOn jacocoTestCoverageVerification
jacocoTestCoverageVerification.dependsOn(jacocoTestReport)

pmd {
    consoleOutput = true
    toolVersion = "6.39.0"
    rulesMinimumPriority = 5
    ruleSets = ["${rootProject.getRootDir()}/qa_config/pmd/ruleset.xml"]
}


tasks.withType(Checkstyle) {
    ignoreFailures = false
    showViolations = true
    config = resources.text.fromFile("${rootProject.getRootDir()}/qa_config/checkstyle/checkstyle.xml")
    reports {
        xml.required = false
        html.required = true
        html.stylesheet resources.text.fromFile("${rootProject.getRootDir()}/qa_config/checkstyle/checkstyle-frames.xsl")
    }
}



